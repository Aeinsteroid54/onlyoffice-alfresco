buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.3.6'
        classpath group: 'com.github.dynamicextensionsalfresco', name: 'gradle-plugin', version: '1.3.0'
        classpath group: 'com.parashift.amp', name: 'amp-plugin', version: '1.0-SNAPSHOT'
    }
}


apply plugin: 'alfresco-dynamic-extension'
apply plugin: 'alfresco-amp'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

ext {
    alfresco = [:]
    alfresco.version = "5.0.1"

    baseName = "onlyoffice-alfresco-repo"
}

project.description = "Onlyoffice Integration"
version = "git describe --tags".execute().text.trim()


repositories {
    mavenCentral()
    maven {
        url "https://maven.alfresco.com/nexus/content/groups/public/"
    }

    maven {
        url "http://repository.springsource.com/maven/bundles/external"
    }

    maven {
        credentials {
            username project['alfresco.repo.private.username']
            password project['alfresco.repo.private.password']
        }

        url "https://maven.alfresco.com/nexus/content/groups/private/"
    }
}

dependencies {
    compile (group: "org.alfresco", name: "alfresco", version: "${alfresco.version}", classifier: "classes") {
        exclude module: "alfresco-web-framework-commons"
        exclude module: "commons-beanutils"
        exclude group: "org.springframework"
        exclude group: "org.mybatis"
    }
    compile "org.alfresco:alfresco-web-framework-commons:${alfresco.version}"

    compile "org.json:json:20141113"

    compile 'org.springframework:spring-context:3.0.0.RELEASE'
    compile 'org.springframework:spring-beans:3.0.0.RELEASE'
    compile 'org.springframework:spring-context-support:3.0.5.RELEASE'

}

jar {
    baseName = project.baseName
    version = project.version

    manifest {
        instructionReplace 'Bundle-Name', 'Onlyoffice Alfresco'
        instruction 'Bundle-Description', project.description
        instruction 'Alfresco-Spring-Configuration', 'com.parashift'
        instruction 'Bundle-DocURL', 'http://www.parashift.com.au'
    }
}

amp {
    id = "com.parashift." + project.baseName
    baseName = project.baseName
    alias = "onlyoffice"
    version = project.version
    title = "Onlyoffice"
    description = project.description

    dependsOn("com.github.dynamicextensionsalfresco:1.0:2.0")
}

alfrescoDynamicExtensions {
    repository {
        endpoint {
            host = project.hasProperty('hostname') ? project.hostname : 'localhost'
            port = '8080'
            serviceUrl = '/alfresco/service'
        }
        authentication {
            username = project.hasProperty('username') ? project.username : 'admin'
            password = project.hasProperty('password') ? project.password : 'admin'
        }
    }
}




